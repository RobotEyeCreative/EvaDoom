#library "convlib"
#include "zcommon.acs"
#import "npclib.acs"


// ------------- Start-End conversation functions, set and reset the NPC potrait and some general effects.


// Conversation hud display properties
#define NoTime 32000.0
#define IDStart 20
#define XCoord 0.15
#define YCoord 0.4
#define ResponseYOffs 0.025
#define AnswersGap 0.2
#libdefine MaxResponses 8

/*
	HudMessage ID definitions for conversations:
	IDStart +
	
	-3	---------------------	Potrait
	-2	---------------------	Conversation actor name
	-1	---------------------	Conversation actor text message
	1 to MaxResponses	-----	Conversation answer option message

*/

Function void ConversationStart (int who) {
	
	// Fade in.
	CancelFade();
	FadeTo(0, 0, 0, 0.5, 3.0);
	
	// Print potrait.
	SetFont(GetNpcPotrait(who));
	HudMessage(s:"A"; HUDMSG_FADEINOUT, IDStart-3, 0, 0.8, 0.5, NoTime, 0.4);
	
	// Print name.
	SetFont("SMALLFONT");
	HudMessage(s:GetNpcName(who); HUDMSG_PLAIN, IDStart-2, CR_GOLD, -XCoord, YCoord-ResponseYOffs, 0);
	
	// PlaySound(0, "interaction/paper");
}

Function void ConversationEnd (void) {
	
	// Clear all text.
	for (int i = -3; i <= MaxResponses; i++)
		HudMessage(s:""; HUDMSG_PLAIN, IDStart+i, 0, 0.0, 0.0, 1);
	
	CancelFade();
	FadeTo(0, 0, 0, 0.0, 0.4);
	SetFont("SMALLFONT");
}

Function void PrintNPCMessage (int text) {

	SetFont("SMALLFONT");
	HudMessage(l:text; HUDMSG_TYPEON, IDStart-1, CR_WHITE, -XCoord, YCoord, NoTime, 0.025, 0.4); //0.05
}


// ------------- Conversation behavioral functions, defines how the text will display and the logic.


int IDCounter = 1;
int YCoordResponse = YCoord + AnswersGap;
int ResponsesBuffer[MaxResponses];

Function void PrintOptionsMessages (str text) {

	SetFont("SMALLFONT");
	
	HudMessage (l:text; HUDMSG_PLAIN, IDStart+IDCounter++, CR_WHITE, -XCoord, YCoordResponse, 0);
	YCoordResponse += ResponseYOffs;
}

Function void ResetOptionsMessagesCoords (void) {
	
	YCoordResponse = YCoord + AnswersGap;
	IDCounter = 1;
}


int ArrowIndex = 1;
int ResponsesBufferCount;
int YCoordArrow;

Function void PrintArrowConversation (int direction) {
	
	switch (direction) {
		case 1:
			if (ArrowIndex < ResponsesBufferCount) ArrowIndex++; // If there's a next option to select go on
			break;
		case 2:
			if (ArrowIndex > 1) ArrowIndex--; // If there's a next option to select go on
			break;
		case 666:
			ArrowIndex = 1; // Reset the arrow position.
	}
	
	// Update arrow Y position.
	YCoordArrow = YCoord + AnswersGap + (ResponseYOffs * (ArrowIndex-1));
	
	SetFont("SMALLFONT");
	HudMessage(s:">"; HUDMSG_PLAIN, IDStart, CR_WHITE, -XCoord+0.01, YCoordArrow, 0);
}

Function int GetResponse (void) {
	
	return ResponsesBuffer[ArrowIndex-1]; // Return the actually selected response.
}

