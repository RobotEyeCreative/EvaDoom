

// TODO: View height only updates when changing your z height. (BUG?)
// TODO: Mugshot changes have to be handled through zscript hud.
// TODO: Switch falling damage.

class evangelionPlayer : PlayerPawn {
	
	int restTime;
	int moveTime;
	bool rightFeet;
	
	string lastWeapon;
	
	mixin atfieldhandling;
	
	default {
		
		//$Category Evangelion
		//$Title "EvangelionPlayer"
		
		health 100;
		player.MaxHealth 100;
		player.DisplayName "EvangelionPlayer";
		player.FallingScreamSpeed 100.0, 110.0;
		
		player.SoundClass "shinji";
		mass 90;
		player.AirCapacity 5;
		player.ViewHeight 40;
		player.AttackZoffset 16;
		player.ForwardMove 0.5, 0.7;
		player.SideMove 0.4, 0.5;
		player.JumpZ 8.0;
		player.ViewBob 0.1;
		player.face "P01";
		
		//player.weaponslot 1, "Eva01Fists";
		//player.weaponslot 2, "Eva01HandGun";
		//player.weaponslot 3, "Eva01AssaultRifle";
		
		//player.startitem "Eva01Fists", 1;
		//player.startitem "Eva01HandGun", 1;
		player.startitem "Bullets", 0;
		player.startitem "HeavyBullets", 0;
		player.startitem "evaenergy", 0;
		player.startitem "evasynchronization", 0;
		player.startitem "atfield", 0;
		player.startitem "enterEva00", 0;
		player.startitem "enterEva01", 0;
		player.startitem "enterEva02", 0;
		
		damagefactor "falling", 0.0;
		
		//+NOBLOOD;
		//+NOTELEFRAG;
		//+PLAYERPAWN.NOTHRUSTWHENINVUL;
	}

	void spawnFeet () {
		
		if (CountInv("playerIdentity") <= 3 && floorz == self.pos.z) {
			
			if (rightFeet) {
				A_StartSound("EvaDoom/EvaUnit/Run01");
				A_SpawnItemEx("evafeetdecal", 0.0, 9.0, 0.0, 0.0, 0.0, 0.0, 0);
			}
			else {
				A_StartSound("EvaDoom/EvaUnit/Run02");
				A_SpawnItemEx("evafeetdecal", 0.0, -9.0, 0.0, 0.0, 0.0, 0.0, 0);
			}
		}
		
		rightFeet = !rightFeet;
	}

	int getFeetTics () {
		
		if (evaspeed > 15.0)
			return 4;
		else if (evaspeed > 10.0)
			return 5;
		else if (evaspeed > 5.0)
			return 6;
		
		return max(6, 16-int(ceil(evaspeed)));
	}
	
	states {
		
		Spawn:
			PLAY A 4;
			Loop;
		See:
			PLAY AB 6 A_SetTics(getFeetTics());
			PLAY B 0 spawnFeet();
			PLAY CD 6 A_SetTics(getFeetTics());
			PLAY D 0 spawnFeet();
			Loop;
		Missile:
			PLAY E 12;
			Goto Spawn;
		Melee:
			PLAY F 6 BRIGHT;
			Goto Missile;
		Pain:
			PLAY G 4;
			PLAY G 4 A_Pain;
			Goto Spawn;
		Death:
			PLAY H 0 A_PlayerSkinCheck("AltSkinDeath");
		Death1:
			PLAY H 10;
			PLAY I 10 A_PlayerScream;
			PLAY J 10 A_NoBlocking;
			PLAY KLM 10;
			PLAY N -1;
			Stop;
		XDeath:
			PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath");
		XDeath1:
			PLAY O 5;
			PLAY P 5 A_XScream;
			PLAY Q 5 A_NoBlocking;
			PLAY RSTUV 5;
			PLAY W -1;
			Stop;
		AltSkinDeath:
			PLAY H 6;
			PLAY I 6 A_PlayerScream;
			PLAY JK 6;
			PLAY L 6 A_NoBlocking;
			PLAY MNO 6;
			PLAY P -1;
			Stop;
		AltSkinXDeath:
			PLAY Q 5 A_PlayerScream;
			PLAY R 0 A_NoBlocking;
			PLAY R 5 A_SkullPop;
			PLAY STUVWX 5;
			PLAY Y -1;
			Stop;
	}
}

class evafeetdecal : actor {

	default {

		radius 6;
		height 1;
		renderstyle "Translucent";
		alpha 0.8;
		scale 0.4;

		-SOLID;
		+NOGRAVITY;
		+NOBLOCKMAP;
		+MOVEWITHSECTOR;
		+FLATSPRITE;
	}

	override void PostBeginPlay() {

		super.PostBeginPlay();

		if (floorz < self.pos.z)
			SetState(null);

		A_Explode(50, 6, XF_EXPLICITDAMAGETYPE, FALSE, 0, 0, 0, "", "EvaSplashDamage");
	}

	states {

		spawn:
			FEET A 140;
			FEET A 1 A_FadeOut(0.01);
			wait;
	}
}