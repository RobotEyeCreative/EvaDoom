
class Eva01HandGun : eva01weapon {
	
	double recoil;

	default {
		
		//$Title "HandGun"
		
		weapon.ammouse 1;
		weapon.ammotype "HeavyBullets";
		weapon.ammogive 30;
		weapon.slotnumber 2;
		//weapon.readysound "";
		//weapon.upsound "";

		+WEAPON.NOAUTOFIRE;
	}
	
	states {

		spawn:
			PIST A -1;
			loop;
		ready:
			E1HG A 1 A_WeaponReady();
			loop;
		select:
			E1HG A 1 A_Raise(12);
			loop;
		deselect:
			E1HG A 1 A_Lower(12);
			loop;
		fire:
			E1HG A 1 {
				A_Overlay(-1001, "Flash");
				A_OverlayFlags(-1001, PSPF_RENDERSTYLE|PSPF_ALPHA, true);
				A_OverlayRenderStyle(-1001, STYLE_Translucent);
				A_OverlayAlpha(-1001, 0.8);
			}
			E1HG A 0 {
				invoker.recoil = 15.0;
				A_OverlayOffset(1, OverlayX(0)+(invoker.recoil/2), OverlayY(0)+invoker.recoil);
				A_StartSound("EvaDoom/Weapon/HandGun/Fire");
				A_FireProjectile("EvaHeavyBullet", frandom(-0.35,0.35), 1, 4.0, 0, FPF_NOAUTOAIM, frandom(-0.35,0.35));
				A_SetPitch(pitch-frandom(0.1,0.3), SPF_INTERPOLATE);
				A_SetAngle(angle+frandom(-0.25,0.25), SPF_INTERPOLATE);
				invoker.recoil = 3;
			}
			E1HG AAAAA 1 {
				A_OverlayOffset(1, OverlayX(0)-(invoker.recoil/2), OverlayY(0)-invoker.recoil, WOF_INTERPOLATE);
			}
			goto ready;
		flash:
			E1HG N 1 A_Light(15);
			goto LightDone;
	}
}