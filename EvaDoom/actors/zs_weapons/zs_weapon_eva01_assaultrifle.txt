
class Eva01AssaultRifle : eva01weapon {
	
	double recoil;

	default {
		
		//$Title "Assault rifle"
		
		weapon.ammouse 1;
		weapon.ammotype "Bullets";
		weapon.ammogive 100;
		weapon.slotnumber 3;
		//weapon.readysound "";
		//weapon.upsound "";
	}
	
	states {
		
		spawn:
			E1RF Z -1;
			loop;
		ready:
			E1RF A 1 A_WeaponReady();
			loop;
		select:
			E1RF A 1 A_Raise(12);
			loop;
		deselect:
			E1RF A 1 A_Lower(12);
			loop;
		fire:
			E1RF A 0 {
				A_Overlay(-1001, "Flash");
				A_OverlayFlags(-1001, PSPF_RENDERSTYLE|PSPF_ALPHA, true);
				A_OverlayRenderStyle(-1001, STYLE_Translucent);
				A_OverlayAlpha(-1001, 0.8);
			}
			E1RF A 1 {
				invoker.recoil = 12.0;
				A_OverlayOffset(1, OverlayX(0)+(invoker.recoil/4), OverlayY(0)+invoker.recoil);
				A_StartSound("EvaDoom/Weapon/HandGun/Fire");
				A_FireProjectile("EvaBullet", frandom(-1.5,1.5), 1, 4.0, 0, FPF_NOAUTOAIM, frandom(-1.5,1.5));
				A_SetPitch(pitch-frandom(0.3,0.8), SPF_INTERPOLATE);
				A_SetAngle(angle+frandom(-0.5,0.5), SPF_INTERPOLATE);
				invoker.recoil = 3.0;
			}
			E1RF AAAA 1 {
				A_OverlayOffset(1, OverlayX(0)-(invoker.recoil/4), OverlayY(0)-invoker.recoil, WOF_INTERPOLATE);
			}
			E1RF A 0 A_OverlayOffset(1, 0.0);
			E1RF A 0 A_ReFire();
			goto ready;
		flash:
			E1RF F 1 A_Light(15);
			goto LightDone;
	}
}